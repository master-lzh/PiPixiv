
import com.mrl.pixiv.buildsrc.configureDesktopSentryMapping
import com.mrl.pixiv.buildsrc.configureRemoveKoinMeta
import org.jetbrains.compose.desktop.application.dsl.TargetFormat
import org.jetbrains.compose.desktop.application.tasks.AbstractJPackageTask
import org.jetbrains.compose.desktop.application.tasks.AbstractProguardTask

plugins {
    id("pixiv.multiplatform.compose")
    alias(libs.plugins.composeHotReload)
}

val desktopOsName = System.getProperty("os.name").toString()
val (mmkvNativeLibraryName, mmkvNativeLibraryDependency) = when {
    desktopOsName == "Mac OS X" ->
        "libmmkvc.dylib" to libs.mmkv.kotlin.nativelib.macos

    desktopOsName.startsWith("Windows") ->
        "mmkvc.dll" to libs.mmkv.kotlin.nativelib.windows

    desktopOsName.startsWith("Linux") ->
        "libmmkvc.so" to libs.mmkv.kotlin.nativelib.linux

    else -> error("Unsupported desktop OS: $desktopOsName")
}

val mmkvNativeLibrary = configurations.create("mmkvNativeLibrary") {
    description = "MMKV native library used only as an input to the Compose app resources task"
    isCanBeConsumed = false
    isCanBeResolved = true
    isTransitive = false
}

dependencies {
    add(mmkvNativeLibrary.name, mmkvNativeLibraryDependency)
}

val composeResourcesDirectory =
    layout.projectDirectory.dir("src/commonMain/composeResources")
val mmkvComposeResourcesDirectory = composeResourcesDirectory.dir("files/mmkv")
val packagedMMKVLibraryPath = "composeResources/files/mmkv/$mmkvNativeLibraryName"

val copyMMKVNativeLibraryToComposeResources =
    tasks.register("copyMMKVNativeLibraryToComposeResources", Copy::class) {
        group = "build"
        description = "Copies the current platform's MMKV native library into Compose app resources"

        val nativeLibraryArchives = mmkvNativeLibrary.incoming.files.elements.map { artifacts ->
            artifacts.map { zipTree(it.asFile) }
        }
        from(nativeLibraryArchives) {
            include(mmkvNativeLibraryName)
        }
        into(mmkvComposeResourcesDirectory)

        doLast {
            val copiedLibrary = destinationDir.resolve(mmkvNativeLibraryName)
            check(copiedLibrary.isFile) {
                "MMKV native library $mmkvNativeLibraryName was not found in ${mmkvNativeLibrary.files}"
            }
        }
    }

if (findProperty("applyFirebasePlugins") == "true") {
    pluginManager.apply(libs.plugins.sentry.kmp.get().pluginId)
}

kotlin {
    android {
        namespace = "com.mrl.pixiv.multiplatform"
    }

    iosArm64()
    iosSimulatorArm64()

    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":lib_strings"))
                implementation(project(":common:ai"))
                implementation(project(":common:data"))
                implementation(project(":common:datasource-local"))
                implementation(project(":common:network"))
                implementation(project(":common:repository"))
                implementation(project(":common:ui"))
                implementation(project(":common:core"))
                rootDir.resolve("feature").listFiles()?.filter { it.isDirectory }?.forEach {
                    implementation(project(":feature:${it.name}"))
                }
                implementation(libs.bundles.compose.navigation3)
                // Coil3
                implementation(project.dependencies.platform(libs.coil3.bom))
                implementation(libs.bundles.coil3)
                // FileKit
                implementation(libs.filekit.core)
                // MMKV
                implementation(libs.mmkv.kotlin)
            }
        }
        androidMain {
            dependencies {
                // Navigation3
                implementation(libs.bundles.compose.navigation3.android)
            }
        }
        iosMain {
            dependencies {

            }
        }
        jvmMain {
            dependencies {
                implementation(compose.desktop.currentOs)
            }
        }
    }

    configureRemoveKoinMeta()
}

compose.desktop {
    application {
        mainClass = "com.mrl.pixiv.MainKt"

        nativeDistributions {
            includeAllModules = true
            targetFormats(
                *listOfNotNull(
                    TargetFormat.Dmg,
                    TargetFormat.Msi,
                    if ("Mac" !in System.getProperty("os.name")) TargetFormat.AppImage else null
                ).toTypedArray()
            )
            packageName = rootProject.name
            packageVersion = findProperty("versionName")?.toString()
            windows {
                iconFile.set(file("icons/pipixiv.ico"))
                shortcut = true
                perUserInstall = true
                msiPackageVersion = findProperty("versionName")?.toString()
                upgradeUuid = "650ae9c7-32ad-400e-93f3-6b0874eccc1c"
                menuGroup = rootProject.name
            }
            linux {
                iconFile.set(file("icons/pipixiv.png"))
                shortcut = true
            }
            macOS { iconFile.set(file("icons/pipixiv.icns")) }
        }

        buildTypes.release.proguard {
            version = "7.9.1"
        }

        jvmArgs("--enable-native-access", "ALL-UNNAMED")
    }
}

val prepareDesktopAppResources = tasks.withType<Sync>().matching { it.name == "prepareAppResources" }
prepareDesktopAppResources.configureEach {
    dependsOn(copyMMKVNativeLibraryToComposeResources)
    // Include a real file in the installer; loading MMKV must never extract resources at startup.
    from(composeResourcesDirectory) {
        include("files/mmkv/$mmkvNativeLibraryName")
        into("composeResources")
    }
}

val verifyMMKVNativeLibraryPackaging = tasks.register("verifyMMKVNativeLibraryPackaging") {
    group = "verification"
    description = "Checks that MMKV is available as a native distribution resource"
    dependsOn(prepareDesktopAppResources)

    val libraryFile = providers.provider {
        prepareDesktopAppResources.single().destinationDir.resolve(packagedMMKVLibraryPath)
    }
    inputs.file(libraryFile)

    doLast {
        val library = libraryFile.get()
        check(library.isFile && library.length() > 0) {
            "MMKV native library is missing from the desktop distribution resources: $library"
        }
    }
}

tasks.withType<AbstractJPackageTask>().configureEach {
    dependsOn(verifyMMKVNativeLibraryPackaging)
}

tasks.matching { it.name == "copyNonXmlValueResourcesForCommonMain" }.configureEach {
    dependsOn(copyMMKVNativeLibraryToComposeResources)
}

val directJvmRunTasks = setOf("jvmRun", "hotRunJvm", "hotDevJvm")
tasks.withType(JavaExec::class.java).configureEach {
    if (name in directJvmRunTasks) {
        dependsOn(copyMMKVNativeLibraryToComposeResources)
        systemProperty(
            "compose.application.resources.dir",
            composeResourcesDirectory.asFile.parentFile.absolutePath,
        )
    }
}

tasks.matching { it.name == "hotRunJvmAsync" || it.name == "hotDevJvmAsync" }.configureEach {
    dependsOn(copyMMKVNativeLibraryToComposeResources)
}

logger.quiet("debug: ${findProperty("debug")}")

if (findProperty("debug") != "true") {
    afterEvaluate {
        val proguardReleaseJars = tasks.named<AbstractProguardTask>("proguardReleaseJars")
        proguardReleaseJars.configure {
            doFirst {
                layout.buildDirectory.file("compose/binaries/main-release/proguard")
                    .get().asFile.mkdirs()
            }
        }
        configureDesktopSentryMapping(
            proguardTask = proguardReleaseJars,
            mappingFile = layout.buildDirectory.file("compose/binaries/main-release/proguard/mapping.txt"),
            outputJar = proguardReleaseJars.flatMap { task ->
                task.mainJar.flatMap { jar -> task.destinationDir.file(jar.asFile.name) }
            },
            organization = "pipixiv",
            sentryProject = "pipixiv",
        )
    }

    tasks.withType(AbstractProguardTask::class.java) {
        val proguardFile = File.createTempFile("tmp", ".pro", temporaryDir)
        proguardFile.deleteOnExit()

        compose.desktop.application.buildTypes.release.proguard {
            configurationFiles.from(proguardFile, file("compose-desktop.pro"))
            optimize = false // fixme(tarsin): proguard internal error
            // Sentry restores these names using the mapping UUID embedded before packaging.
            obfuscate = true
            joinOutputJars = true
        }

        doFirst {
            proguardFile.bufferedWriter().use { proguardFileWriter ->
                sourceSets["jvmMain"].runtimeClasspath
                    .filter { it.extension == "jar" }
                    .forEach { jar ->
                        val zip = zipTree(jar)
                        zip.matching { include("META-INF/**/proguard/*.pro") }.forEach {
                            proguardFileWriter.appendLine("########   ${jar.name} ${it.name}")
                            proguardFileWriter.appendLine(it.readText())
                        }
                        zip.matching { include("META-INF/services/*") }.forEach {
                            it.readLines().forEach { cls ->
                                val rule = "-keep class $cls"
                                proguardFileWriter.appendLine(rule)
                            }
                        }
                    }
            }
        }
    }
} else {
    compose.desktop.application.buildTypes.release.proguard {
        isEnabled = false
    }
}

fun ipaArguments(
    destination: String = "generic/platform=iOS",
    sdk: String = "iphoneos",
): Array<String> {
    return arrayOf(
        "xcodebuild",
        "-project", "PiPixiv.xcodeproj",
        "-scheme", "PiPixiv",
        "-destination", destination,
        "-sdk", sdk,
        "CODE_SIGNING_ALLOWED=NO",
        "CODE_SIGNING_REQUIRED=NO",
    )
}

val buildReleaseArchive = tasks.register("buildReleaseArchive", Exec::class) {
    group = "build"
    description = "Archives the iOS app with the Swift Export package"
    workingDir(rootDir.resolve("iosApp"))

    val output = layout.buildDirectory.dir("archives/release/PiPixiv.xcarchive")
    outputs.dir(output)
    commandLine(
        *ipaArguments(),
        "archive",
        "-configuration", "Release",
        "-archivePath", output.get().asFile.absolutePath,
    )
}

@CacheableTask
abstract class BuildIpaTask : DefaultTask() {

    /* -------------------------------------------------------------
     * Inputs / outputs
     * ----------------------------------------------------------- */

    @get:InputDirectory
    @get:PathSensitive(PathSensitivity.ABSOLUTE)
    abstract val archiveDir: DirectoryProperty

    @get:OutputFile
    abstract val outputIpa: RegularFileProperty

    /* -------------------------------------------------------------
     * Services (injected)
     * ----------------------------------------------------------- */

    @get:Inject
    abstract val execOperations: ExecOperations

    /* -------------------------------------------------------------
     * Action
     * ----------------------------------------------------------- */

    @TaskAction
    fun buildIpa() {
        // 1. Locate the .app inside the .xcarchive
        val appDir = archiveDir.get().asFile.resolve("Products/Applications/PiPixiv.app")
        if (!appDir.exists())
            throw GradleException("Could not find PiPixiv.app in archive at: ${appDir.absolutePath}")

        // 2. Create temporary Payload directory and copy .app into it
        val payloadDir = File(temporaryDir, "Payload").apply { mkdirs() }
        val destApp = File(payloadDir, appDir.name)
        appDir.copyRecursively(destApp, overwrite = true)

        // 3. Inject placeholder (ad‑hoc) code signature so AltStore / SideStore accept it
        logger.lifecycle("[IPA] Ad‑hoc signing ${destApp.name} …")
        execOperations.exec {
            commandLine(
                "codesign", "--force", "--deep", "--sign", "-", "--timestamp=none",
                destApp.absolutePath,
            )
        }

        // 4. Zip Payload ⇒ .ipa using the system `zip` command
        //
        //    -r : recurse into directories
        //    -y : store symbolic links as the link instead of the referenced file
        //
        // The working directory is the temporary folder so the archive
        // has a top‑level "Payload/" directory (required for .ipa files).
        val zipFile = File(temporaryDir, "PiPixiv.zip")
        execOperations.exec {
            workingDir(temporaryDir)
            commandLine("zip", "-r", "-y", zipFile.absolutePath, "Payload")
        }

        // 5. Move to final location (with .ipa extension)
        outputIpa.get().asFile.apply {
            parentFile.mkdirs()
            delete()
            zipFile.renameTo(this)
        }

        logger.lifecycle("[IPA] Created ad‑hoc‑signed IPA at: ${outputIpa.get().asFile.absolutePath}")
    }
}

tasks.register("buildReleaseIpa", BuildIpaTask::class) {
    description = "Manually packages the .app from the .xcarchive into an unsigned .ipa"
    group = "build"

    // Adjust these paths as needed
    archiveDir = layout.buildDirectory.dir("archives/release/PiPixiv.xcarchive")
    outputIpa = layout.buildDirectory.file("archives/release/PiPixiv.ipa")
    dependsOn(buildReleaseArchive)
}
